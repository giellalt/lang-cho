!! Verb inflection
!  ---------------
!! The Choctaw language verbs inflect for person, tense, mood, and other things

! Stem list at end of file
! Lexicons in this file (in order): Subj/Obj/Dat prefixes (I, II, III)
! Verb type taggers (add flag diacritics for I-II, I-III, etc.)
! Causative suffix (WIP)
! Suffixes/flaggers for subj, obj, dat
! Tense suffixes (WIP)
! Verb stems (may be moved)

LEXICON V-I-II ! Most common
  @U.ARG.I-II@ V-subj ;

LEXICON V-I-III ! Somewhat common
  @U.ARG.I-III@ V-subj ;

LEXICON V-II-III ! Rare
  @U.ARG.II-III@ V-subj ;

LEXICON V-II-II ! Rare
  @U.ARG.II-II@ V-subj ;

LEXICON V-III-II ! Incredibly rare (possibly only one verb, no more than 4)
  @U.ARG.III-II@ V-subj ;

LEXICON V-Intrans-I
  @U.TRANS.F@@U.ARG.I@ V-subj-intrans ;

LEXICON V-Intrans-II
  @U.TRANS.F@@U.ARG.II@ V-subj-intrans ;

LEXICON V-Intrans-III
  @U.TRANS.F@@U.ARG.III@ V-subj-intrans ;

! VERB TYPES: I,II (most common); I,III (pretty common);
! II,II; III,II; II,III (biggest of smaller three)

! LEXICON V-caus
  ! @U.CAUS.T@+Caus:@U.CAUS.T@^chi tense ;
  ! @D.CAUSE@ tense ;
! TODO--fix causative

LEXICON V-subj  ! 1Sg-I is a suffix, others nothing
  @U.SUBJ.1SG@@U.ARG.I-II@+V+1Sg:@U.SUBJ.1SG@@U.ARG.I-II@^li V-obj ;
  @U.SUBJ.1SG@@U.ARG.I-III@+V+1Sg:@U.SUBJ.1SG@@U.ARG.I-III@^li V-obj ;
  @U.SUBJ.1SG@@D.ARG.I-II@@D.ARG.I-III@+V+1Sg:@U.SUBJ.1SG@@D.ARG.I-II@@D.ARG.I-III@ V-obj ;
  @U.SUBJ.2SG@+V+2Sg:@U.SUBJ.2SG@ V-obj ;
  @U.SUBJ.1PL@+V+1Pl:@U.SUBJ.1PL@ V-obj ;
  @U.SUBJ.1PAUC@+V+1Pauc:@U.SUBJ.1PAUC@ V-obj ;
  @U.SUBJ.1MULT@+V+1Mult:@U.SUBJ.1MULT@ V-obj ;
  @U.SUBJ.2PL@+V+2Pl:@U.SUBJ.2PL@ V-obj ;
  @U.SUBJ.UNSP@+V+Unsp:@U.SUBJ.UNSP@ V-obj ;

LEXICON V-subj-intrans
  @U.SUBJ.1SG@@U.ARG.I@+V+1Sg:@U.SUBJ.1SG@@U.ARG.I@^li V-tense ;
  @U.SUBJ.1SG@@D.ARG.I@+V+1Sg:@U.SUBJ.1SG@@D.ARG.I@   V-tense ;
  @U.SUBJ.2SG@+V+2Sg:@U.SUBJ.2SG@                     V-tense ;
  @U.SUBJ.1PL@+V+1Pl:@U.SUBJ.1PL@                     V-tense ;
  @U.SUBJ.1PAUC@+V+1Pauc:@U.SUBJ.1PAUC@               V-tense ;
  @U.SUBJ.1MULT@+V+1Mult:@U.SUBJ.1MULT@               V-tense ;
  @U.SUBJ.2PL@+V+2Pl:@U.SUBJ.2PL@                     V-tense ;
  @U.SUBJ.UNSP@+V+Unsp:@U.SUBJ.UNSP@                  V-tense ;

LEXICON V-obj
  @U.OBJ.1SG@+1Sg:@U.OBJ.1SG@       V-dat ;
  @U.OBJ.2SG@+2Sg:@U.OBJ.2SG@       V-dat ;
  @U.OBJ.1PAUC@+1Pauc:@U.OBJ.1PAUC@ V-dat ;
  @U.OBJ.1MULT@+1Mult:@U.OBJ.1MULT@ V-dat ;
  @U.OBJ.2PL@+2Pl:@U.OBJ.2PL@       V-dat ;
  @U.OBJ.UNSP@+Unsp:@U.OBJ.UNSP@    V-dat ;

LEXICON V-dat
  @U.DAT.1SG@+1Sg:@U.DAT.1SG@       V-mod ;
  @U.DAT.2SG@+2Sg:@U.DAT.2SG@       V-mod ;
  @U.DAT.1PAUC@+1Pauc:@U.DAT.1PAUC@ V-mod ;
  @U.DAT.1MULT@+1Mult:@U.DAT.1MULT@ V-mod ;
  @U.DAT.2PL@+2Pl:@U.DAT.2PL@       V-mod ;
  @U.DAT.UNSP@+Unsp:@U.DAT.UNSP@    V-mod ;
  @U.DAT.NONE@                      V-mod ;

LEXICON V-mod
  +Irr:^aachiN V-tense ; ! more often with affirm
  +Irr:^ahii   V-tense ; ! more often with neg
  +Pot:^aana   V-tense ;
  +Pot:^ahiNla V-tense ;
  0 V-tense ; ! realis

! ahiina, ayna, aN = dialectical/idiolectical variants of ahiNla
! sometimes ahii-h becomes ahii'

LEXICON V-tense ! TODO: add complementizers
  +Tns:^h        V-grade ;
  +DPst:^ttook   V-grade ; ! distant past
  +Pst:^to%{kp%} V-grade ;
! TODO--other verb tenses

LEXICON V-grade
  +NGrade:^%{ng%}   # ;
  +HNGrade:^%{hng%} # ;
  +LGrade:^%{lg%}   # ;
  +HGrade:^%{hg%}   # ;
  +GGrade:^%{gg%}   # ;
  +YGrade:^%{yg%}   # ;
  0 # ;
