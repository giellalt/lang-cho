!! Verb inflection
!  ---------------
!! The Choctaw language verbs inflect for person, tense, mood, and other things

! Stem list at end of file
! Lexicons in this file (in order): Subj/Obj/Dat prefixes (I, II, III)
! Verb type taggers (add flag diacritics for I-II, I-III, etc.)
! Causative suffix (WIP)
! Suffixes/flaggers for subj, obj, dat
! Tense suffixes (WIP)
! Verb stems (may be moved)

LEXICON V-I-II ! Most common
  @U.S.I@@U.O.II@:@U.S.I@@U.O.II@+ V-subj ;

LEXICON V-I-III ! Somewhat common
  @U.S.I@@U.O.III@:@U.S.I@@U.O.III@+ V-subj ;

LEXICON V-II-III ! Rare
  @U.S.II@@U.O.III@:@U.S.II@@U.O.III@+ V-subj ;

LEXICON V-II-II ! Rare
  @U.S.II@@U.O.II@:@U.S.II@@U.O.II@+ V-subj ;

LEXICON V-III-II ! Incredibly rare (possibly only one verb, no more than 4)
  @U.S.III@@U.O.II@:@U.S.III@@U.O.II@+ V-subj ;

LEXICON V-Intrans-I
  @U.O.N@@U.S.I@:@U.O.N@@U.S.I@+ V-subj ;

LEXICON V-Intrans-II
  @U.O.N@@U.S.II@:@U.O.N@@U.S.II@+ V-subj ;

LEXICON V-Intrans-III
  @U.O.N@@U.S.III@:@U.O.N@@U.S.III@+ V-subj ;

! VERB TYPES: I,II (most common); I,III (pretty common);
! II,II; III,II; II,III (biggest of smaller three)

! LEXICON V-caus
  ! @U.CAUS.T@+Caus:@U.CAUS.T@^chi tense ;
  ! @D.CAUSE@ tense ;
! TODO--fix causative

LEXICON V-subj  ! 1Sg-I is a suffix, others nothing
  @D.N@@U.SUBJ.1SG@@U.S.I@+V+1Sg:@D.N@@U.SUBJ.1SG@@U.S.I@^l%{i%} Neg ;
  @D.SUBJ.1SG@@U.S.I@ Neg ;
  @D.S.I@ Neg ;

LEXICON Neg
  @P.LREQ.T@@R.N.T@@U.HORT.F@+Neg:@P.LREQ.T@@R.N.T@@U.HORT.F@%{d%}^o%{k%}  V-mod ;
  @P.LREQ.T@@R.N.T@@U.HORT.F@+Neg:@P.LREQ.T@@R.N.T@@U.HORT.F@%{d%}^okii   V-mod ;
  @R.N.T@@U.HORT.T@  V-mod ;!
  @D.N@                      V-mod ;


LEXICON V-mod
  +Irr:^aachiN  Neg2 ; ! more often with affirm
  +Irr:^ahii   Neg2 ; ! more often with neg
  +Pot:^aana   Neg2 ;
  +Pot:^ahiNla Neg2 ;
  @P.REAL.T@ V-tense ; ! realis

LEXICON Neg2
  @D.N@+Neg:@D.N@^kiiyo V-tense ; ! TODO: set negative to true
  V-tense ; ! non-negative, or internally negated

! ahiina, ayna, aN = dialectical/idiolectical variants of ahiNla
! sometimes ahii-h becomes ahii'

LEXICON V-tense ! TODO: add complementizers
  @P.H.T@+Tns:@P.H.T@^h        EIF ;
  @P.K.T@+Tns2:@P.K.T@^k EIF ;
  +DPst:^ttook   EIF ; ! distant past
  +Pst:^to%{kp%} EIF ;
  +Cert:^hli V-grade ; ! certainty
  +Cert:^hlik V-grade ;
  +Cert:^hnik V-grade ;
  +Part:^t # ; ! participial marker
! TODO--other verb tenses

LEXICON EIF ! TODO: constrain: can't precede a negative
  @R.K.T@+Cert:@R.K.T@^aamo V-grade ;
  @R.K.T@+Cert:@R.K.T@^hchaamo V-grade ;
  +Prob:^a V-grade ; ! probably, I guess
  +Doubt:^ba V-grade ; ! I doubt
  +Wonder:^chi V-grade ; ! I wonder, I'm not sure if
  +Wonder:^chichook V-grade ; ! I wonder, I'm not sure if
  +Untrue:^chiit V-grade ; ! I think it's false that... He's not ?? is he??
  +Suppose:^chiichi V-grade ; ! I suppose, lack of commitment
  +Suppose:^chiichik V-grade ; ! ^^^
  +Unknown:^chihba V-grade ; ! I don't know if...
  +Unknown:^chishba V-grade ; ! ^^^
  +Guess:^kaNsha V-grade ; ! I guess
  +Affirm:^k`a V-grade ; ! I'm telling you! (Japanese -yo)
  +Affirm:^kat V-grade ;
  +Affirm:^kato V-grade ;
  +Emph:^kiaN V-grade ; ! emphatic
  +Warn:^taN V-grade ; ! warning
  +Indeed:^okii V-grade ; ! emphatic, indeed, often pronounced "okee"
  +Exhort:^ashkii V-grade ; ! exhortation
  +Q:^oN V-grade ;
  @R.H.T@+Q:@R.H.T@^aN V-grade ;
  @R.REAL.T@+Q:@R.REAL.T@^ka' V-grade ;
  +Q:^fo V-grade ;
  +Q2:^cho V-grade ;
  +Insult:^mat V-grade ;
  @P.LREQ.T@+Insult:@P.LREQ.T@^cha V-grade ;
  +Exclaim:^kat V-grade ;
  +Exclaim:^kato V-grade ;
  @U.SUBJ.IMP@+Imp:@U.SUBJ.IMP@cha V-grade ;
  @U.SUBJ.IMP@+Imp:@U.SUBJ.IMP@^oo V-grade ;
  @U.SUBJ.IMP@+Imp:@U.SUBJ.IMP@ V-grade ;
  @U.SUBJ.2SG@+Prohib:@U.SUBJ.2SG@nn`a V-grade ;
  @U.SUBJ.2PL@+Prohib:@U.SUBJ.2PL@nn`a V-grade ;
  V-grade ; ! nothing

LEXICON V-grade
  @D.LREQ@+NGrade:@D.LREQ@^%{ng%}   # ;
  @D.LREQ@+HNGrade:@D.LREQ@^%{hng%} # ;
  @R.LREQ.T@+LGrade:@R.LREQ.T@^%{lg%}   # ;
  @D.LREQ@+HGrade:@D.LREQ@^%{hg%}   # ;
  @D.LREQ@+GGrade:@D.LREQ@^%{gg%}   # ;
  @D.LREQ@+YGrade:@D.LREQ@^%{yg%}   # ;
  @D.LREQ@ # ;
