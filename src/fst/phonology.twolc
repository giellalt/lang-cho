!! =================================== !
!! The Choctaw morphophonological/twolc rules file !
!! =================================== !

Alphabet
 a b ch f h i k l lh m n o p s sh t w y %' `
 A B Ch F H I K L Lh M N O P S Sh T W Y
 aa ii oo aN iN oN
 Aa Ii Oo !AN IN ON

 %^:0
 %$:0

 %{mN%}:m
 %{ai%}:a
 %{il%}:il
 %{s%}:s
 %{kp%}:k ! stands for k-past, used for the past tense
 %{a%}:a
 %{k%}:k
 %{nom%}:at
 %{acc%}:aN

! aspectual grades
 %{ng%}:0
 %{hng%}:0
 %{lg%}:0
 %{hg%}:0
 %{gg%}:0
 %{yg%}:0
;

Sets

 Vow = a i o aa ii oo aN iN oN ;
 LVow = aa ii oo aN iN oN ;
 NVow = aN iN oN aN iN oN;
 NNVow = a i o aa ii oo ;
 Cns = b ch f h k l lh m n p s sh t w y z %' ;
 Case = %{nom%} %{acc%} ;
 Letter = Cns Vow;

Rules

"Glottal stop -> y before case endings"
  %':y <=> _ %^: Case: ;

"il -> ii before a consonant"
  %{il%}:ii <=> _ %^: Cns: ;

"k -> 0 before another k in past tense"
  %{kp%}:0 <=> _ %^: k: ;

"m -> N in III prefixes before consonant"
  %{mN%}:N <=> _ %^: Cns: ;

"s -> 0 in sa- word initially"
  %{s%}:0 <=> .#. _ ;

"Only one ` allowed"
  `:0 <=> _ ?* `: ;

"n-grade 1"
  Vx:Vy <=> _ \Vow:* Vow: [\Vow: - %^:]* %^: ?* %{ng%}: ;
           where Vx in NNVow
                 Vy in NVow
           matched ;

"n-grade 2"
  Cx:Cy <=> _ Vow: \Vow:* Vow: [\Vow: - %^:]* %^: ?* %{ng%}: ;
           where Cx in (a aa aN b ch f h i ii iN k l lh m n o oo oN p s sh t w y ')
                 Cy in (a` aa` aN` b` ch` f` h` i` ii` iN` k` l` lh` m` n` o` oo` oN` p` s` sh` t` w` y` '`)
           matched ;
