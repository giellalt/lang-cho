!! =================================== !
!! The Choctaw morphophonological/twolc rules file !
!! =================================== !

Alphabet
 a b ch f h i k l lh m n o p s sh t w y %' `
 A B Ch F H I K L Lh M N O P S Sh T W Y
 aa ii oo
 aN iN oN
 %^:0
 %+:0
 
 %{i%}:i
 %{d%}:0
 %{mN%}:m
 %{ai%}:a
 %{il%}:il
 %{s%}:s
 %{kp%}:k ! stands for k-past, used for the past tense
 %{a%}:a
 %{k%}:k
 %{nom%}:at
 %{acc%}:aN

! aspectual grades
 %{ng%}:0
 %{hng%}:0
 %{lg%}:0
 %{hg%}:0
 %{gg%}:0
 %{yg%}:0
;

Sets

 SVow = a i o ;
 LVow = aa ii oo ;
 Vow = SVow LVow;
 NVowM = aN iN oN aN iN oN ;
 NVow = aN iN oN ;
 AVow = Vow NVow ;
 Cns = b ch f h k l lh m n p s sh t w y %' ;
 NonVow = Cns ` ;
 Case = %{nom%} %{acc%} ;
 Letter = Cns Vow ;
 Grade =  %{ng%} %{hng%} %{lg%} %{hg%} %{gg%} %{yg%} ;

 Definitions
 VV = [Vow: | :Vow] ;
 AnyVow = ([`: | :`]) [AVow: | :AVow] ;
 Syllable = [\AVow: | \:AVow]* AnyVow [\AVow: | \:AVow]* ;

! syllable = NVow:* Vx: [NVow: Vow: | Vow: - Vx:] ;
! where Vx in Vow ;

Rules

"Glottal stop -> y before case endings"
  %':y <=> _ %^: Case: ;

"il -> ii before a consonant"
  %{il%}:ii <=> _ %^: Cns: ;

"k -> 0 before another k in past tense"
  %{kp%}:0 <=> _ %^: k: ;

"m -> N in III prefixes before consonant"
  %{mN%}:N <=> _ %^: Cns: ;

"s -> 0 in sa- word initially"
  %{s%}:0 <=> .#. _ ;

"Only one ` allowed"
  `:0 <=> _ ?* :` ;


! GRADES:
! TODO: handle shift in pitch accent
"n-grade"
 Vx:Vy <=> %+:0 ?* _ \:AVow* AnyVow \:AVow* %+:0 \Grade:* %{ng%}: .#. ;
       where Vx in Vow
             Vy in NVowM
       matched ;

"l-grade, g-grade vow"
 Vx:Vy <=> %+:0 ?* _ \:AVow* AnyVow \:AVow* %+:0 \Grade:* [%{lg%}: | %{gg%}:] .#. ;
      where Vx in SVow 
            Vy in LVow
      matched ;

"h-grade"
 0:h <=> %+:0 ?* AVow: _ [\:AVow - :h]* AnyVow \:AVow* %+:0 \Grade:* %{hg%}: .#. ;

"hn-grade"
 0:Vx <=> %+:0 ?* Vy: _ [\:AVow - 0:Vx]* AnyVow \:AVow* %+:0 \Grade:* %{hng%}: .#. ;
      where Vy in AVow
            Vx in (h`aN h`iN h`oN h`aN h`iN h`oN h`aN h`iN h`oN)
      matched ;

"g-grade cons"
 0:Cx <=> %+:0 [?* - ?* \Cx:] Cx:Cx _ AnyVow \AVow:* AnyVow \AVow:* %+:0 \Grade:* %{gg%}: .#. ; 
      where Cx in Cns ;

"y-grade"
 0:Vx <=> %+:0 ?* Vy: _ \:AVow* AnyVow \:AVow* %+:0 \Grade:* %{yg%}: .#. ;
      where Vy in AVow
            Vx in (yya yyi yyo yya yyi yyo yya yyi yyo)
      matched ;

"orthographic fix for long + N" ! I think this won't work
  Vx:0 <=> _ :N ;
            where Vx in LVow ;

"Delete vowel before -ok suffix"
 Vow:0 <=> _ %+:0 \%+:* %{d%}:0 ;

"Delete -i of -li when followed by a vowel"
 %{i%}:0 <=> _ (%^:) :Vow ;

"k deleted before another consonant"
 %{k%}:0 <=> _ (%^:) Cns: ;
