!! =================================== !
!! The Choctaw morphophonological/twolc rules file !
!! =================================== !

Alphabet
 a b ch f h i k l lh m n o p s sh t w y %' `
 A B Ch F H I K L Lh M N O P S Sh T W Y

 %^:0
 %>:0

 %{mN%}:m
 %{ai%}:a
 %{il%}:il
 %{s%}:s
 %{kp%}:k ! stands for k-past, used for the past tense
 %{a%}:a
 %{k%}:k
 %{nom%}:at
 %{acc%}:aN

! aspectual grades
 %{ng%}:0
 %{hng%}:0
 %{lg%}:0
 %{hg%}:0
 %{gg%}:0
 %{yg%}:0
;

Sets

 Vow = a i o;
 Cns = b ch f h k l lh m n p s sh t w y z %' ;
 NVow = Cns ` ;
 Case = %{nom%} %{acc%} ;
 Letter = Cns Vow ;
 Grades =  %{ng%} %{hng%} %{lg%} %{hg%} %{gg%} %{yg%} ;

 Definitions

 Penultimate = NVow:* Vow: (Vow:) Cns:* %^: [\Grades: - %^:]* ;

Rules

"Glottal stop -> y before case endings"
  %':y <=> _ %^: Case: ;

"il -> ii before a consonant"
  %{il%}:ii <=> _ %^: Cns: ;

"k -> 0 before another k in past tense"
  %{kp%}:0 <=> _ %^: k: ;

"m -> N in III prefixes before consonant"
  %{mN%}:N <=> _ %^: Cns: ;

"s -> 0 in sa- word initially"
  %{s%}:0 <=> .#. _ ;

"Only one ` allowed"
  `:0 <=> _ ?* `: ;

"n/l/h/g-grade accent"
  0:` <=> .#. \%^:* \:` _ (Vow:0) Vow: :N Penultimate %{ng%}: .#. ;
          .#. \%^:* \:` _ Vow: :Vow Penultimate %{lg%}: .#. ;
          .#. \%^:* \:` _ Vow: ([:Vow | :N]) Penultimate %{hg%}: .#. ; 
          .#. \%^:* \:` _ Vow: ([:Vow | :N]) :NVow* Vow: ([:Vow | :N]) Penultimate %{gg%}: .#. ;
          .#. \%^:* \:` _ 0:Vow 0:yy (:`) Vow: ([:Vow | :N]) Penultimate %{yg%}: .#. ;
!          0:h _ 0:Vow 0:N Penultimate %{hng%}: .#. ;

"n/hn-grade lengthening and nasalization"
  0:N <=> .#. \%^:* :Vow _ Penultimate %{ng%}: .#. ;
!         .#. \%^:*  0:h 0:` 0:Vow _ Penultimate %{hng%}: .#. ;

!"hn-grade h insertion"
! 0:h <=> .#. \%^:* :Vow _ 0:` 0:Vow 0:N Penultimate %{hng%}: .#. ;

!"hn-grade vowel insertion"
! 0:Vx <=> .#. \%^:* Vx: 0:h 0:` _ 0:N Penultimate %{hng%}: .#. ;
!             where Vx in Vow ;   

"hn-grade"
 0:Vx <=> .#. \%^:* Vy: _ Penultimate %{hng%}: .#. ;
             where Vx in (h`aN h`iN h`oN)
                   Vy in Vow
             matched ;

"orthographic fix for long + N"
  Vx:0 <=> _ :Vx :N ;
            where Vx in Vow ;

"l/y-grade lengthening"
  0:Vx <=> .#. \%^:* Vx _ Penultimate %{lg%}: .#. ;
           .#. \%^:* \0:Vx _ 0:yy (:`) Vx: ([:Vx | :N]) Penultimate %{yg%}: .#. ;
             where Vx in Vow ;

"h-grade h-insertion"
  0:h <=> .#. \%^:* 0:` Vow: ([:Vow | :N]) _ Penultimate %{hg%}: .#. ;

"g-grade gemination"
  0:Cx <=> .#. \%^:* \:Cx Cx:Cx _ Vow: ([:Vow | :N]) Penultimate %{gg%}: .#. ;
           where Cx in Cns ;

"y-grade yy insertion"
  0:yy <=> .#. \%^:* \0:yy _ Vow: ([:Vow | :N]) Penultimate %{yg%}: .#. ;
