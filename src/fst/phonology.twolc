!! =================================== !
!! The Choctaw morphophonological/twolc rules file !
!! =================================== !

Alphabet
 a b ch f h i k l lh m n o p s sh t w y %' `
 A B Ch F H I K L Lh M N O P S Sh T W Y

 %^:0
 %>:0
 %+:0
 
 %{i%}:i
 %{d%}:0
 %{mN%}:m
 %{ai%}:a
 %{il%}:il
 %{s%}:s
 %{kp%}:k ! stands for k-past, used for the past tense
 %{a%}:a
 %{k%}:k
 %{nom%}:at
 %{acc%}:aN

! aspectual grades
 %{ng%}:0
 %{hng%}:0
 %{lg%}:0
 %{hg%}:0
 %{gg%}:0
 %{yg%}:0
;

Sets

 Vow = a i o;
 Cns = b ch f h k l lh m n p s sh t w y %' ;
 NVow = Cns ` ;
 Case = %{nom%} %{acc%} ;
 Letter = Cns Vow ;
 Grade =  %{ng%} %{hng%} %{lg%} %{hg%} %{gg%} %{yg%} ;

 Definitions

 AnyVow = ([`: | :`]) :Vow ([:Vow | :N]) ;
 Syllable = NVow:* AnyVow Cns:* ;
 Penultimate = NVow:* AnyVow Cns:* %^: [\Grade: - %^:]* ;

Rules

"Glottal stop -> y before case endings"
  %':y <=> _ %^: Case: ;

"il -> ii before a consonant"
  %{il%}:ii <=> _ %^: Cns: ;

"k -> 0 before another k in past tense"
  %{kp%}:0 <=> _ %^: k: ;

"m -> N in III prefixes before consonant"
  %{mN%}:N <=> _ %^: Cns: ;

"s -> 0 in sa- word initially"
  %{s%}:0 <=> .#. _ ;

"Only one ` allowed"
  `:0 <=> _ ?* :` ;


! GRADES:
! TODO: handle shift in pitch accent
"n-grade"
 0:N <=> %+:0 ?* \:Vx :Vx _ \:N \:Vow* AnyVow \:Vow* %+:0 \Grade:* %{ng%}: .#. ;
         where Vx in Vow ;

"l-grade, g-grade vow"
 0:Vx <=> %+:0 ?* \:Vx Vx: _ [\:Vx & :Vow] \:Vow* AnyVow \:Vow* %+:0 \Grade:* [%{lg%}: | %{gg%}:] .#. ;
          %+:0 ?* \:Vx Vx: _ :Cns \:Vow* AnyVow \:Vow* %+:0 \Grade:* [%{lg%}: | %{gg%}:] .#. ;
      where Vx in Vow ;

"h-grade"
 0:h <=> %+:0 ?* \:h _ AnyVow \:Vow* AnyVow \:Vow* %+:0 \Grade:* %{hg%}: .#. ;

"hn-grade"
 0:Vx <=> %+:0 ?* Vy: ([:Vy | :N]) _ [:Cns* AnyVow | [\:Vy & Vow]] \:Vow* %+:0 \Grade:* %{hng%}: .#. ;
      where Vy in Vow
            Vx in (h`aN h`iN h`oN)
      matched; 

"g-grade cons"
 0:Cx <=> %+:0 [?* - ?* \:Cx] Cx:Cx _ AnyVow \:Vow* AnyVow \:Vow* %+:0 \Grade:* %{gg%}: .#. ;
      where Cx in Cns ;

"y-grade"
 0:Vx <=> %+:0 ?* Vy: ([:Vy | :N]) _ [:Cns* AnyVow | [\:Vy & Vow]] \:Vow* %+:0 \Grade:* %{yg%}: .#. ;
      where Vy in Vow
            Vx in (yya yyi yyo)
      matched ;

"orthographic fix for long + N"
  Vx:0 <=> _ :Vx :N ;
            where Vx in Vow ;

"Delete vowel before -ok suffix"
 Vow:0 <=> _ %+:0 \%+:* %{d%}:0 ;

"Delete -i of -li when followed by a vowel"
 %{i%}:0 <=> _ (%^:) Vow: ;
